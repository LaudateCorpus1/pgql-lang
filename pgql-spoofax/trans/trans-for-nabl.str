module trans-for-nabl

imports

  stratego/-
  signatures/-
  normalized-signatures
  common
  pp
  runtime/index/-
  runtime/nabl/-
  runtime/properties/-
  runtime/types/-
  runtime/task/-
  runtime/analysis/-
  runtime/editor/-
  names/-

/*
 * 'trans-for-nabl' is called after 'normalize' and transforms the normalized AST into a shape that works for NaBL.
 *
 *  PGQL String -> PGQL AST -> PGQL normalized AST -> PGQL AST for NaBL -> GraphQuery object -> query plan
 */
rules

  trans-for-nabl = is-ddl-statement
                 + trans-query(|[])
                 + ?Start-Plhdr() // for empty query string

rules

  trans-query(|variables):
    NormalizedQuery(CommonPathExpressions(pathExpressions), selectOrModifyClause, optionalGraphName, graphPattern, groupBy, having, orderBy, limitOffsets, error-messages, version, bindVariableCount) ->
        NormalizedQuery(CommonPathExpressions(pathExpressions'), selectOrModifyClause', optionalGraphName, graphPattern'', groupBy', having', orderBy', limitOffsets, error-messages, version, bindVariableCount)
    with variables' := <guarantee-size-two-or-more> variables

       // PATH
       ; pathExpressions' := <map(trans-path-expression(|variables'))> pathExpressions

       // these expAsVars are used to make the column aliases in SELECT visible in WHERE and GROUP BY
       ; expAsVars := <?SelectClause(_, ExpAsVars(<id>)) <+ ![]> selectOrModifyClause

       ; if <?Some(GraphPattern(vertices, connections, valueExpression))> := graphPattern
         then // MATCH
              (vertices', connections') := <alltd(trans-elem(|variables'))> (vertices, connections)
              ; edges := <filter(?Edge(_, _, _, _, _, _) + path-with-at-most-one-binding)> connections'
              ; paths := <filter(path-with-two-or-more-bindings)> connections'

              ; new-vars := <collect(to-varDef)> (vertices', edges)
              ; new-groupVars := <collect(to-varDef)> paths
              ; <?[x|[y|ys]]> variables'
              ; visible-vars := <replace-or-add-all> (new-vars, x)
              ; visible-groupVars := <replace-or-add-all> (new-groupVars, y)
              ; variables'' := [visible-vars|[visible-groupVars|ys]]

              ; connections'' := <alltd(resolve-var-refs-in-path-expression(|variables''))> connections'
       
              // WHERE
              ; valueExpression' := <resolve-value-expression(|variables'', expAsVars)> valueExpression

              ; graphPattern' := Some(GraphPattern(vertices', connections'', valueExpression'))
         else graphPattern' := None()
            ; variables'' := variables'
         end

       // GROUP BY
       ; if (!groupBy; ?Some(_)) + (!selectOrModifyClause; create-one-group(|variables'')) + (!having; ?Some(_))
         then hasGroupBy := True()
            ; (groupBy', variables''') := <resolve-group-by(|variables'', expAsVars)> groupBy
         else hasGroupBy := False()
            ; (groupBy', variables''') := (groupBy, variables'')
         end

       // SELECT / MODIFY
       ; if <?SelectClause(_, _)> selectOrModifyClause
         then (selectOrModifyClause', variables'''') := <resolve-select-clause(|variables''')> selectOrModifyClause
         else selectOrModifyClause' := <resolve-modify-clause(|variables''')> selectOrModifyClause
           ; variables'''' := variables'''
        end

       // HAVING
       ; having' := <resolve-having(|variables''', variables'''')> having // having resolves to GROUP BY variables first, then to SELECT variables

       // ORDER BY
       // resolve to SELECT variables first, then to GROUP BY variables (except in case of a VarRef in a PropRef, in which case it is resolved to MATCH or GROUP BY first)
       ; orderBy' := < resolve-var-refs(|variables'''')
                     ; resolve-prop-refs(|variables''')
                     ; alltd(optimize-order-by)> orderBy

       ; if <?Some(GraphPattern(vertices', connections'', valueExpression'))> := graphPattern'
         then connections''' := <map(try(normalize-ANY-path(|variables'''')))> connections'' // normalize ANY either to REACHES or SHORTEST
            ; graphPattern'' := Some(GraphPattern(vertices', connections''', valueExpression'))
         else graphPattern'' := graphPattern'
         end

  path-with-at-most-one-binding:
    t@Path(_, _, _, quantifier, _, _, _, _) -> t
    where <has-at-most-one-binding> quantifier

  path-with-two-or-more-bindings:
    t@Path(_, _, _, quantifier, _, _, _, _) -> t
    where not( <has-at-most-one-binding> quantifier )

  guarantee-size-two-or-more = ?[]; ![[], []] <+ ?[x]; ![x, []] <+ id

  trans-elem(|variables):
    Vertex(iden@Identifier(v, _)) -> Vertex(iden, origin-offset, correlation)
    with origin-offset := <origin-offset> v
       ; correlation := <to-Correlation(|variables)> v

  trans-elem(|variables):
    NormalizedEdge(src, iden@Identifier(e, _), dst, direction, _, _) -> Edge(src, iden, dst, direction, origin-offset, correlation)
    with origin-offset := <origin-offset> e
       ; correlation := <to-Correlation(|variables)> e

  to-Correlation(|variables):
    v -> correlation
    with if <Hd; oncetd(?VarDef(Identifier(v, _), _, _, _, _, _); ?VarDef(identifier-from-outer-query, origin-position-from-outer-query, _, _, _, _))> variables
         then correlation := Correlation(VarRef(identifier-from-outer-query, origin-position-from-outer-query))
         else correlation := None()
         end

  to-varDef = ?Vertex(iden, origin-offset, _); !VarDef(iden, origin-offset, None(), None(), None(), ThisQuery())
  to-varDef = ?Edge(_, identifier, _, _, origin-offset, _); !VarDef(identifier, origin-offset, None(), None(), None(), ThisQuery())

  replace-or-add-all = foldl(replace-or-add)

  replace-or-add:
    (vd@VarDef(Identifier(v, _), _, _, _, _, _), varDefs) -> varDefs'
    with if <fetch(?VarDef(Identifier(v, _), _, _, _, _, _))> varDefs
         then varDefs' := <conc> (<remove-all(?VarDef(Identifier(v, _), _, _, _, _, _))> varDefs, [vd])
         else varDefs' := <conc> (varDefs, [vd])
         end

  resolve-value-expression(|variables, expAsVars):
    valueExpression -> valueExpression'
    with valueExpression' := <resolve-var-refs-and-use-expAsVarsFromSelectClause(|variables, expAsVars)> valueExpression

  resolve-group-by(|variables, expAsVarsFromSelectClause):
    Some(GroupByClause(expAsVars)) -> (Some(GroupByClause(expAsVars')), variables')
    with (expAsVars', vars') := <foldl(resolve-exp-as-var-in-group-by(|variables, expAsVarsFromSelectClause))> (expAsVars, ([], []))
       ; variables' := [vars'|variables]

  resolve-group-by(|variables, expAsVars):
    None() -> (CreateOneGroup(), variables')
    with vars' := []
       ; variables' := [vars'|variables]

  resolve-select-clause(|variables):
    t@SelectClause(distinct, eav@ExpAsVars(expAsVars)) -> (selectClause', variables')
    with if Star() := expAsVars
         then eav' := expAsVars
            ; variables':= variables
         else varsInCurrentScope := <Hd> variables
            ; (expAsVars', vars') := <foldl(resolve-exp-as-var-in-select(|variables))> (expAsVars, ([], varsInCurrentScope))
            ; eav' := <origin-track-forced(!ExpAsVars(expAsVars'))> eav
            ; variables' := [vars'|<Tl> variables]
         end
       ; selectClause' := <origin-track-forced(!SelectClause(distinct, eav'))> t

  resolve-modify-clause(|variables):
    t@ModifyClause(modifications) -> modifyClause'
    with varsInCurrentScope := <Hd> variables
       ; modifications' := <map(resolve-modification(|variables))> modifications
       ; modifyClause' := <origin-track-forced(!ModifyClause(modifications'))> t

  resolve-modification(|variables):
    t@InsertClause(graphName, insertions) -> insertClause'
    with (insertions', _) := <foldl(resolve-insertion(|variables))> (insertions, ([], variables))
       ; insertClause' := <origin-track-forced(!InsertClause(graphName, insertions'))> t

  resolve-modification(|variables) = ?UpdateClause(_); resolve-var-refs(|variables)

  resolve-modification(|variables) = ?DeleteClause(_); resolve-var-refs(|variables)

  resolve-insertion(|original-variables):
    (t, (result, variables)) -> (result', variables')
    where <?VertexInsertion(iden@Identifier(v, _), labels, properties) + ?DirectedEdgeInsertion(iden@Identifier(v, _), src, dst, labels, properties)> t
    with originOffset := <origin-offset> v
       ; properties' := <resolve-set-properties(|v, originOffset, original-variables)> properties
       ; if <?VertexInsertion(_, _, _)> t
         then insertion := <origin-track-forced(!VertexInsertion(iden, originOffset, labels, properties'))> t
         else src' := <resolve-var-refs(|variables)> src
            ; dst' := <resolve-var-refs(|variables)> dst
            ; insertion := <origin-track-forced(!DirectedEdgeInsertion(iden, originOffset, src', dst', labels, properties'))> t
         end
       ; [vars-in-scope|xs] := variables
       ; vars-in-scope' := <replace-or-add> (VarDef(iden, originOffset, None(), None(), None(), ThisQuery()), vars-in-scope)
       ; variables' := [vars-in-scope'|xs]
       ; result' := <conc> (result, [insertion])

  resolve-set-properties(|v, originOffset, original-variables) = ?None() + Some(Properties(map(resolve-set-property(|v, originOffset, original-variables))))

  resolve-set-property(|v, originOffset, original-variables):
    t@SetProperty(PropRef(varRef, prop), exp) -> result
    with varRef' := <?VarRef(Identifier(v, _)); ?VarRef(<id>); !VarRef(<id>, originOffset) <+ resolve-var-refs(|original-variables)> varRef
       ; exp' :=  <origin-track-forced(resolve-var-refs(|original-variables))> exp
       ; result := <origin-track-forced(!SetProperty(PropRef(varRef', prop), exp'))> t

  resolve-exp-as-var-in-group-by(|variables, expAsVarsFromSelectClause):
    (t@ExpAsVar(exp, iden@Identifier(v, _), anonymous), (result, vars)) -> (result', vars')
    with exp' := <resolve-var-refs-and-use-expAsVarsFromSelectClause(|variables, expAsVarsFromSelectClause)> exp
       ; replaced-groupByExp-with-selectExp := <alltd(?VarRef(<id>, _); !VarRef(<id>))> exp'
       ; originOffset := <origin-offset> v
       ; vars' := <replace-or-add> (VarDef(iden, originOffset, exp, replaced-groupByExp-with-selectExp, exp', ThisQuery()), vars)
       ; expAsVar' := <origin-track-forced(!ExpAsVar(exp', iden, anonymous, originOffset))> t
       ; result' := <conc> (result, [expAsVar'])

  resolve-exp-as-var-in-select(|variables):
    (t@ExpAsVar(exp, iden@Identifier(v, _), anonymous), (result, vars)) -> (result', vars')
    with exp' := <resolve-var-refs(|variables)> exp
       ; originOffset := <origin-offset> v
       ; if <?Anonymous(); !v; origin-text; ?"*"> anonymous
         then // in case of SELECT * make sure each ExpAsVar gets a unique origin assigned
              unique-origin := <conc-strings> ("*_", <write-to-string> originOffset, "_", <write-to-string> v)
         else unique-origin := originOffset
         end
       ; vars' := <replace-or-add> (VarDef(iden, unique-origin, exp, None(), exp', ThisQuery()), vars)
       ; expAsVar' := <origin-track-forced(!ExpAsVar(exp', iden, anonymous, unique-origin))> t
       ; result' := <conc> (result, [expAsVar'])

  resolve-having(|variables-after-group-by, variables-after-select):
    having -> having'
    with [x|xs] := variables-after-select
       ; [y|_] := variables-after-group-by
       ; z := <replace-or-add-all> (y, x)
       ; variables := [z|xs]
       ; having' := <resolve-var-refs(|variables)> having

  resolve-var-refs(|variables) =
      alltd(resolve-var-ref(|variables))
    ; detect-common-subexpressions(|variables)

  /*
     First try to resolve variables as usual.
     For example, in "SELECT n.prop AS m FROM MATCH (n) -> (m) WHERE m.prop = 123", the "m" in the WHERE clause should reference the "m" in the MATCH clause.
     Only if it fails, try to replace variables with expressions from SELECT before trying to resolve again.
     For example, in "SELECT n.prop AS m FROM MATCH (n) WHERE m.prop = 123", the "m" in the WHERE clause should reference the "m" in the SELECT clause.
     Note that in the final AST, we only want the SELECT to refer to variables in GROUP BY, even though for the user it typically appears like it's the other way arround.
     For example:
        Actual query: SELECT n.age + 2 FROM MATCH (n) GROUP BY n.age + 2
        Final AST:    SELECT generatedVar FROM MATCH (n) GROUP BY n.age + 2 AS generatedVar
     For example:
        Actual query: SELECT (n.age + 2)*(n.age + 2) FROM MATCH (n) GROUP BY n.age + 2
        Final AST:    SELECT generatedVar*generatedVar FROM MATCH (n) GROUP BY n.age + 2 AS generatedVar
     For example:
        Actual query: SELECT n.age + 2 AS v FROM MATCH (n) GROUP BY v
        Final AST:    SELECT v FROM MATCH (n) GROUP BY n.age + 2 AS v
  */
  resolve-var-refs-and-use-expAsVarsFromSelectClause(|variables, expAsVarsFromSelectClause) =
      alltd-in-outer-query-outside-aggregation( resolve-var-ref-within-query(|variables, expAsVarsFromSelectClause);
                                                try(replace-ref-with-exp(|variables, expAsVarsFromSelectClause)))
    ; alltd(resolve-var-ref(|variables)) // now also resolve subqueries and also resolve to variables from outer query
    ; detect-common-subexpressions(|variables)

  detect-common-subexpressions(|variables) =
      alltd(replace-exp-with-ref-within-this-query(|variables) <+ is-subquery <+ is-aggregate)
    ; alltd(replace-exp-with-ref-from-outer-queries(|variables) <+ is-subquery <+ is-aggregate)

  resolve-var-refs-in-path-expression(|variables):
    t@Path(_, _, _, quantifier, _, _, _, _) -> t'
    with [_|variables'] := variables
       ; if <has-at-most-one-binding> quantifier
       then t' := <alltd(resolve-var-ref(|variables))> t // resolve only to singleton variables
       else t' := <alltd(resolve-var-ref(|variables') + resolve-var-ref(|variables))> t // resolve to either group variables or singleton variables
        end

  resolve-var-ref(|variables):
    t@VarRef(iden@Identifier(v, _)) -> varRef
    with varRef := <
           Hd; fetch(?VarDef(Identifier(v, _), origin-offset, _, _, _, _)); !VarRef(iden, origin-offset)
           <+ !VarRef(iden)
         > variables

  // within query only, so don't resolve to variables from outer queries
  resolve-var-ref-within-query(|variables, expAsVarsFromSelectClause):
  t@VarRef(iden@Identifier(v, _)) -> varRef
    with varRef := <
           Hd; fetch(?VarDef(Identifier(v, _), origin-offset, _, _, _, ThisQuery())); !VarRef(iden, origin-offset)
           <+ !VarRef(iden)
         > variables

  resolve-var-ref(|variables):
    t@PropRef(VarRef(iden@Identifier(v, _)), prop) -> PropRef(varRef, prop)
    with varRef := <
           Hd
         ; fetch(?VarDef(Identifier(v, _), origin-offset, original-exp,_ , _, _))

          // The original expression needs to be a vertex/edge defintion (i.e. ?None()) or a reference to a vertex/edge definition (i.e. ?VarRef(_)).
          // Otherwise, it is a more complex ExpAsVar but we don't want to resolve a VarRef of a PropRef to anything other than a vertex or edge.
          // So, we leave it unresolved for now. Possibly, it will get resolved by "resolve-prop-refs" later.
         ; where ( <?None() + ?VarRef(_)> original-exp )

         ; !VarRef(iden, origin-offset)

        <+ !VarRef(iden)
      > variables

  // MIN, MAX, SUM, AVG, ...
  resolve-var-ref(|variables):
    aggr -> <origin-track-forced(!aggr')> aggr
    where <is-aggregate> aggr
    with (cons, arguments) := <explode-term> aggr
       ; variables' := <Tl> variables
       ; arguments' :=  <resolve-var-refs(|variables')> arguments
       ; aggr' := <mkterm> (cons, arguments')

  resolve-var-ref(|variables):
    Subquery(query) -> Subquery(query')
    with variables' := <alltd(VarDef(id, id, id, id, id, !OuterQuery()))> variables
       ; query' := <trans-query(|variables')> query

  replace-exp-with-ref-within-this-query(|variables):
    exp -> varRef
    where not ( None() := exp )
        ; varRef := <Hd; filter(?VarDef(_, _,_ , _, _, ThisQuery())); fetch-elem(replace-unresolved-exp-with-ref(|exp) + replace-resolved-exp-with-ref-helper(|exp))> variables

  replace-exp-with-ref-from-outer-queries(|variables):
    exp -> varRef
    where not ( None() := exp )
        ; varRef := <Hd; filter(?VarDef(_, _,_ , _, _, OuterQuery())); fetch-elem(replace-unresolved-exp-with-ref(|exp))> variables

  /*
     When the expression could not be resolved, but there is an equivalent expression that we can replace it with.
     For example:
       Actual query: SELECT n.age FROM g MATCH (n) GROUP BY n.age
       Final AST:    SELECT generatedVar FROM g MATCH (n) GROUP BY n.age AS generatedVar
  */
  replace-unresolved-exp-with-ref(|exp) = ?VarDef(iden, origin-offset, original-exp, _, _, _); where ( <eq-ignore-case> (exp, original-exp) ); !VarRef(iden, origin-offset)

  /*
     When the expression could not be resolved, but there is an equivalent expression in the SELECT and GROUP BY.
     For example:
       Actual query: SELECT n.age AS nAge MATCH (n) GROUP BY nAge ORDER BY nAge
       Final AST:    SELECT nAge FROM g MATCH (n) GROUP BY n.age AS nAge ORDER BY nAge
  */
  replace-unresolved-exp-with-ref(|exp) = ?VarDef(iden, origin-offset, _, replaced-groupByExp-with-selectExp, _, _); where ( <eq-ignore-case> (exp, replaced-groupByExp-with-selectExp) ); !VarRef(iden, origin-offset)

  /*
     When the expression was resolved succesfully, but there is an equivalent expression that we can replace it with.
     For example:
       Actual query: SELECT n.age AS nAge FROM g MATCH (n) ORDER BY n.age
       Final AST:    SELECT n.age AS nAge FROM g MATCH (n) ORDER BY nAge
  */
  replace-resolved-exp-with-ref-helper(|exp) = ?VarDef(iden, origin-offset, _, _, resolved-exp, _); where ( <eq-ignore-case> (exp, resolved-exp) ); !VarRef(iden, origin-offset)

  eq-ignore-case = alltd(?Identifier(<id>, _)); eq

  replace-ref-with-exp(|variables, expAsVars):
    VarRef(Identifier(v, _)) -> resolved-exp
    where exp := <filter(?ExpAsVar(<id>, Identifier(v, _), NonAnonymous())); Hd> expAsVars
    with resolved-exp := <alltd(resolve-var-ref(|variables))> exp

  resolve-prop-refs(|variables) = alltd(resolve-prop-ref(|variables))

  resolve-prop-ref(|variables):
    t@PropRef(varRef@VarRef(_), prop) -> PropRef(varRef', prop)
    with varRef' := <resolve-var-ref(|variables)> varRef

  /*
     if the query has no GROUP BY but there are one ore more aggregations in the SELECT, then we generate an
     implicit group, but only if there is no (non-nested) aggregation over a group variable
  */
  create-one-group(|variables):
    t@select-or-modify-clause -> t
    with [vars|[groupVars|_]] := variables
       ; aggregations := <collect-in-outer-query(is-aggregate)> select-or-modify-clause
       ; exps-from-aggregations := <collect(get-exp-from-aggregation)> select-or-modify-clause
       ; varsInAggregations := <collect-in-outer-query-outside-aggregation(varRef-is-visible-var(|vars))> exps-from-aggregations
       ; groupVarsInAggregations := <collect-in-outer-query-outside-aggregation(varRef-is-visible-var(|groupVars))> exps-from-aggregations
       ; groupVarsInAggregations' := <diff> (groupVarsInAggregations, varsInAggregations)
    where not ( [] := aggregations ) // there are aggregations
        ; [] := groupVarsInAggregations' // but no aggregations that involve group variables (not counting nested aggregations)

  varRef-is-visible-var(|visible-vars):
    t@VarRef(Identifier(v, _)) -> t
    where <oncetd(?VarDef(Identifier(v, _), _, _, _, _, _))> visible-vars

  trans-path-expression(|variables):
    CommonPathExpression(name, vertices, edges, valueExpression, costClause) -> CommonPathExpression(name, vertices', edges', valueExpression', costClause')
    with
       // pattern
        (vertices', edges') := <alltd(trans-elem(|variables))> (vertices, edges)
       ; new-vars := <collect(to-varDef)> (vertices', edges')
       ; <?[x|xs]> variables
       ; visible-vars := <replace-or-add-all> (new-vars, x)

       // WHERE
       ; variables' := [visible-vars|xs]
       ; valueExpression' := <resolve-value-expression(|variables', [])> valueExpression

       ; costClause' := <resolve-value-expression(|variables', [])> costClause

  optimize-order-by:
    OrderByClause(elems) -> OrderByClause(elems')
    with elems' := <make-set-while-preserving-ordering> elems // optimization to translate e.g. ORDER BY x, y, x, y into ORDER BY x, y

  normalize-ANY-path(|variables):
    Path(src, dst, t@CommonPathExpression(_, Vertices(vertices), Edges(edges), _, _), quantifier, edge, direction, Any(), topKAnyAll)
      -> Path(src, dst, t, quantifier, edge, direction, pathFindingGoal, topKAnyAll)
    with groupVariables := <conc> (<map(?Vertex(_, <id>, _))> vertices, <map(?Edge(_, _, _, _, <id>, _))> edges)
       ; references := <Hd; collect(?VarRef(_, <id>))> variables
       ; referencedGroupVariables := <isect> (groupVariables, references)
       ; if <?[]> referencedGroupVariables
         then pathFindingGoal := Reaches()
         else pathFindingGoal := Shortest()
         end
