module type-analysis

imports

  stratego/-
  libspoofax/properties/-
  libspoofax/nabl/-
  libspoofax/task/-
  libspoofax/types/-
  signatures/-
  normalized-signatures
  common

rules

  add-types = is-ddl-statement <+ // we don't perform any type anlaysis for DDL for now
              ?AstPlusMetadata(ast, metadata); !ast; add-types-to-query(|metadata, []); !AstPlusMetadata(<id>, metadata)

  add-types-to-query(|metadata, variablesPlusTypes):
      NormalizedQuery(CommonPathExpressions(pathExpressions), selectOrModifyClause, optionalGraphName, graphPattern, groupBy, having, orderBy, limitOffsets, error-messages, version, bindVariableCount) ->
        NormalizedQuery(CommonPathExpressions(pathExpressions'), selectOrModifyClause', optionalGraphName, graphPattern', groupBy', having', orderBy', limitOffsets, error-messages, version, bindVariableCount)
    with // PATH
         pathExpressions' := <map(add-types-to-path-expression(|metadata))> pathExpressions

       // MATCH
       ; if <?Some(GraphPattern(Vertices(vertices), connections, _))> := graphPattern
         then verticesWithTypes := <collect-vertices-with-types> (vertices, connections)
            ; edgesWithTypes := <collect-edges-with-types> connections
            ; variablesPlusTypes' := <conc> (variablesPlusTypes, verticesWithTypes, edgesWithTypes)
            ; constraints := <collect-in-outer-query(?Constraints(<id>)); concat> graphPattern
            ; graphPattern' := <alltd(add-type(|metadata, variablesPlusTypes', constraints))> graphPattern
         else variablesPlusTypes' := variablesPlusTypes
            ; graphPattern' := graphPattern
         end

         // GROUP BY
       ; if <?Some(_)> groupBy
         then groupBy' := <alltd(add-type(|metadata, variablesPlusTypes', constraints))> groupBy
            ; variablesPlusTypes'' := <conc> (variablesPlusTypes', <?Some(GroupByClause(<id>)); map(expAsVar-to-variablePlusType(|variablesPlusTypes'))> groupBy')
         else groupBy' := groupBy
            ; variablesPlusTypes'' := variablesPlusTypes'
         end

         // SELECT
       ; if <?SelectClause(_, ExpAsVars(_))> selectOrModifyClause
         then selectOrModifyClause' := <alltd(add-type(|metadata, variablesPlusTypes'', constraints))> selectOrModifyClause
            ; variablesPlusTypes''' := <conc> (variablesPlusTypes'', <?SelectClause(_, ExpAsVars(<id>)); map(expAsVar-to-variablePlusType(|variablesPlusTypes''))> selectOrModifyClause')
         else selectOrModifyClause' := selectOrModifyClause
            ; variablesPlusTypes''' := variablesPlusTypes''
         end

         // HAVING
       ; having' := <alltd(add-type(|metadata, variablesPlusTypes''', constraints))> having

         // ORDER BY
       ; orderBy' := <alltd(add-type(|metadata, variablesPlusTypes''', constraints))> orderBy

  collect-vertices-with-types = collect-in-outer-query(?Vertex(_, <id>, _); !VariablePlusType(<id>, <id>, Type("VERTEX")))

  collect-edges-with-types = collect-in-outer-query(?Edge(_, _, _, _, <id>, _); !VariablePlusType(<id>, <id>, Type("EDGE")))

  add-types-to-path-expression(|metadata):
    pathExpression@CommonPathExpression(_, Vertices(vertices), Edges(edges), Constraints(constraints), _) -> pathExpression'
    with verticesWithTypes := <collect-vertices-with-types> vertices
       ; edgesWithTypes := <collect-edges-with-types> edges
       ; variablesPlusTypes := <conc> (verticesWithTypes, edgesWithTypes)
       ; pathExpression' := <alltd(add-type(|metadata, variablesPlusTypes, constraints))> pathExpression

rules // exp AS var

  expAsVar-to-variablePlusType(|variablesPlusTypes):
    ExpAsVar(ExpressionPlusType(exp ,type), _, _, originPosition) -> VariablePlusType(originPosition, dereferencedOriginPosition, type)
    with if <?VarRef(_, varRefOriginPosition)> exp
         then dereferencedOriginPosition := <fetch-elem(?VariablePlusType(varRefOriginPosition, <id>, _))> variablesPlusTypes
         else dereferencedOriginPosition := originPosition
         end

rules // variable references

  add-type(|metadata, variablesPlusTypes, constraints):
    exp@VarRef(_, varRefOriginPosition) -> ExpressionPlusType(exp, type)
    with type := <fetch-elem(?VariablePlusType(varRefOriginPosition, _, <id>)) <+ !UnknownType()> variablesPlusTypes

  // unresolved variable
  add-type(|metadata, variablesPlusTypes, constraints):
    exp@VarRef(_) -> ExpressionPlusType(exp, UnknownType())

rules // property references

  add-type(|metadata, variablesPlusTypes, constraints):
    exp@PropRef(VarRef(_, varRefOriginPosition), Identifier(propName, _)) -> ExpressionPlusType(exp, propType)
    with elementType := <fetch-elem(?VariablePlusType(varRefOriginPosition, dereferencedOriginPosition, <id>)) <+ !UnknownType()> variablesPlusTypes
       ; labelExpression := <filter(simplify-label-expression(|dereferencedOriginPosition)); (?[]; !All() <+ to-label-conjunction)> constraints
       ; propType := <get-property-type-from-label-expression(|elementType, propName, metadata)> labelExpression

  // unresolved variable
  add-type(|metadata, variablesPlusTypes, constraints):
    exp@PropRef(VarRef(_), _) -> ExpressionPlusType(exp, UnknownType())

  simplify-label-expression(|varRefOriginPosition):
    FunctionCall(None(), "has_label", [ VarRef(_, varRefOriginPosition), Identifier(label, _)]) -> label

  simplify-label-expression(|varRefOriginPosition):
    Or(a, b) -> LabelOr(a', b')
    where a' := <simplify-label-expression(|varRefOriginPosition)> a
        ; b' := <simplify-label-expression(|varRefOriginPosition)> b

  to-label-conjunction = reverse; !(<Tl>, <Hd>); foldl(to-label-AND)
  to-label-AND = ?(exp1, exp2); !LabelAnd(exp1, exp2)

  get-property-type-from-label-expression(|elementType, propName, metadata):
    labelExpression -> type
    where <is-string> labelExpression
    with labelReference := labelExpression
       ; if <?Type("VERTEX")> elementType
         then type := <get-vertex-property-type(|metadata)> (labelReference, propName)
         else type := <get-edge-property-type(|metadata)> (labelReference, propName)
         end

  get-property-type-from-label-expression(|elementType, propName, metadata):
    LabelAnd(a, b) -> result
    with result := <compute-union-type(|elementType, propName, metadata)> (a, b)

  get-property-type-from-label-expression(|elementType, propName, metadata):
    LabelOr(a, b) -> result
    with result := <compute-union-type(|elementType, propName, metadata)> (a, b)

  compute-union-type(|elementType, propName, metadata):
    (labelA, labelB) -> result
    with typeA := <get-property-type-from-label-expression(|elementType, propName, metadata)> labelA
       ; typeB := <get-property-type-from-label-expression(|elementType, propName, metadata)> labelB
       ; result := <get-union-type(|metadata)> (typeA, typeB)

  get-property-type-from-label-expression(|elementType, propName, metadata):
    All() -> result
    with labels := <?Type("VERTEX"); get-vertex-labels(|metadata) <+ get-edge-labels(|metadata)> elementType
       ; if <?None()> labels
         then result := UnknownType()
         else types := <map(!(<id>, propName); get-vertex-property-type(|metadata))> labels
            ; result := <foldl(get-union-type(|metadata))> (types, Undefined())
         end

rules // literals

  add-type(|metadata, variablesPlusTypes, constraints) = ?Null(); !ExpressionPlusType(<id>, UnknownType())

  add-type(|metadata, variablesPlusTypes, constraints) = (?True() + ?False()); !ExpressionPlusType(<id>, Type("BOOLEAN"))

  add-type(|metadata, variablesPlusTypes, constraints) = ?Integer(_); !ExpressionPlusType(<id>, <get-default-long-integer-type(|metadata)>)

  add-type(|metadata, variablesPlusTypes, constraints) = ?Decimal(_); !ExpressionPlusType(<id>, <get-default-decimal-type(|metadata)>)

  add-type(|metadata, variablesPlusTypes, constraints) = ?String(_); !ExpressionPlusType(<id>, <get-default-string-type(|metadata)>)

  add-type(|metadata, variablesPlusTypes, constraints) = ?Date(_); !ExpressionPlusType(<id>, Type("DATE"))

  add-type(|metadata, variablesPlusTypes, constraints) = ?Time(_); !ExpressionPlusType(<id>, Type(<has-timezone; !"TIME WITH TIME ZONE" <+ !"TIME">))

  add-type(|metadata, variablesPlusTypes, constraints) = ?Timestamp(_); !ExpressionPlusType(<id>, Type(<has-timezone; !"TIMESTAMP WITH TIME ZONE" <+ !"TIMESTAMP">))

  external has-timezone(|)

rules // functions

  add-type(|metadata, variablesPlusTypes, constraints):
    func@FunctionCall(package, name, args) -> ExpressionPlusType(func', Type("BOOLEAN")) // TODO fix type
    with args' := <alltd(add-type(|metadata, variablesPlusTypes, constraints))> args
       ; func' := <origin-track-forced(!FunctionCall(package, name, args'))> func

rules // subqueries

  add-type(|metadata, variablesPlusTypes, constraints):
    ScalarSubquery(Subquery(query)) -> ExpressionPlusType(ScalarSubquery(Subquery(query')), type)
    with (query', type) := <add-types-to-subquery(|metadata, variablesPlusTypes, constraints)> query

  add-type(|metadata, variablesPlusTypes, constraints):
    Exists(Subquery(query)) -> ExpressionPlusType(Exists(Subquery(query')), type)
    with (query', type) := <add-types-to-subquery(|metadata, variablesPlusTypes, constraints)> query

  add-types-to-subquery(|metadata, variablesPlusTypes, constraints):
    query -> (query', type)
    with query' := <add-types-to-query(|metadata, variablesPlusTypes)> query
       ; if <?NormalizedQuery(_, SelectClause(_, ExpAsVars([ExpAsVar(ExpressionPlusType(_, Type(t)), _, _, _)])), _, _, _, _, _, _, _, _, _)> query'
         then type := Type(t)
         else type := UnknownType()
         end

rules // binary expressions

  add-type(|metadata, variablesPlusTypes, constraints) =
    ( 
      ?And(_, _) + ?Or(_, _) +
      ?Eq(_, _) + ?Neq(_, _) + ?Gt(_, _) + ?Lt(_, _) + ?Gte(_, _) + ?Lte(_, _) +
      ?Mul(_, _) + ?Add(_, _) + ?Div(_, _) + ?Mod(_, _) + ?Sub(_, _) +
      ?Cct(_, _)
    )
    ; add-value-type-to-binary-operation(|metadata, variablesPlusTypes, constraints)

  add-value-type-to-binary-operation(|metadata, variablesPlusTypes, constraints):
    exp -> ExpressionPlusType(exp', type')
    with operation := <get-constructor> exp
       ; args@[ExpressionPlusType(_, type1), ExpressionPlusType(_, type2)] := <get-arguments; alltd(add-type(|metadata, variablesPlusTypes, constraints))> exp
       ; if <?AmbiguousType()> type1 + <?AmbiguousType()> type2
         then type' := UnknownType()
         else if <?Type(t1)> type1; <?Type(t2)> type2
              then type' := <get-operation-return-type(|metadata)> (operation, t1, t2)
              else type' := UnknownType()
              end
         end
       ; exp' := <origin-track-forced(!(operation, args); mkterm)> exp

rules // unary expressions and aggregates

  add-type(|metadata, variablesPlusTypes, constraints):
    n@Not(exp) -> ExpressionPlusType(n', type')
    with exp'@ExpressionPlusType(_, type) := <add-type(|metadata, variablesPlusTypes, constraints)> exp
       ; n' := <origin-track-forced(!Not(exp'))> n
       ; if <?Type(t)> type
         then type' := <get-operation-return-type(|metadata)> ("Not", t)
         else type' := UnknownType()
         end

  add-type(|metadata, variablesPlusTypes, constraints):
    umin@UMin(exp) -> ExpressionPlusType(umin', type')
    with exp'@ExpressionPlusType(_, type) := <add-type(|metadata, variablesPlusTypes, constraints)> exp
       ; umin' := <origin-track-forced(!UMin(exp'))> umin
       ; if <?Type(t)> type
         then type' := <get-operation-return-type(|metadata)> ("UMin", t)
         else type' := UnknownType()
         end

  add-type(|metadata, variablesPlusTypes, constraints):
    aggregation -> ExpressionPlusType(aggregation', type')
    where <?SUM(distinct, exp) +
           ?MIN(distinct, exp) +
           ?MAX(distinct, exp) +
           ?AVG(distinct, exp) +
           ?ARRAY-AGG(distinct, exp) +
           ?LISTAGG(distinct, exp, separator)> aggregation
    with operation := <get-constructor> aggregation
       ; exp'@ExpressionPlusType(_, type) := <add-type(|metadata, variablesPlusTypes, constraints)> exp
       ; if <?AmbiguousType()> type
         then type' := UnknownType()
         else if <?Type(t)> type
              then type' := <get-operation-return-type(|metadata)> (operation, t)
              else type' := UnknownType()
              end
         end
       ; if <?LISTAGG(_, _, _)> aggregation
         then args := [distinct, exp', separator]
         else args := [distinct, exp']
         end
       ; aggregation' := <origin-track-forced(!(operation, args); mkterm)> aggregation

  add-type(|metadata, variablesPlusTypes, constraints) = ?COUNT(_, _); !ExpressionPlusType(<id>, <get-default-long-integer-type(|metadata)>)

rules // other expressions

  add-type(|metadata, variablesPlusTypes, constraints) =
    (
      ?InPredicate(_, _, _) +
      ?IsNull(_)
    )
    ; add-types-to-subterms(|metadata, variablesPlusTypes, constraints); !ExpressionPlusType(<id>, Type("BOOLEAN"))

  add-type(|metadata, variablesPlusTypes, constraints) = ?BindVariable(_); !ExpressionPlusType(<id>, UnknownType())

  add-type(|metadata, variablesPlusTypes, constraints) = ?Cast(_, type); add-types-to-subterms(|metadata, variablesPlusTypes, constraints); !ExpressionPlusType(<id>, Type(type))

  add-type(|metadata, variablesPlusTypes, constraints) = ?CharacterSubstring(_, _, _); add-types-to-subterms(|metadata, variablesPlusTypes, constraints); !ExpressionPlusType(<id>, <get-default-string-type(|metadata)>)

  add-type(|metadata, variablesPlusTypes, constraints) = ?FunctionCall(_, _, _); add-types-to-subterms(|metadata, variablesPlusTypes, constraints); !ExpressionPlusType(<id>, UnknownType()) // TODO

  add-types-to-subterms(|metadata, variablesPlusTypes, constraints):
    t -> t'
    with (constructor, args) := <explode-term> t
       ; args' := <alltd(add-type(|metadata, variablesPlusTypes, constraints))> args
       ; t' := <origin-track-forced(!(constructor, args'); mkterm)> t

  add-type(|metadata, variablesPlusTypes, constraints):
    extractExp@ExtractExp(part, dateTimeExp) -> ExpressionPlusType(extract', type')
    with dateTimeExp' := <add-type(|metadata, variablesPlusTypes, constraints)> dateTimeExp
       ; extract' := <origin-track-forced(!ExtractExp(part, dateTimeExp'))> extractExp
       ; type' := <?Second(); get-default-decimal-type(|metadata) <+ get-default-short-integer-type(|metadata)> part

  add-type(|metadata, variablesPlusTypes, constraints):
    ifElse@IfElse(_, _, _) -> ExpressionPlusType(ifElse', type')
    with ifElse' := <add-types-to-subterms(|metadata, variablesPlusTypes, constraints)> ifElse
       ; if <?IfElse(_, ExpressionPlusType(_, type1@Type(_)), elseExp)> ifElse'
         then if <?None()> elseExp
              then type' := type1
              else type' := <?ExpressionPlusType(_, type2); !(type1, type2); get-union-type(|metadata) <+ !UnknownType()> elseExp
              end
         else type' := UnknownType()
         end


  get-vertex-labels(|metadata) = !None()
  get-edge-labels(|metadata) = !None()

  get-vertex-property-type(|metadata) = !UnknownType()
  get-edge-property-type(|metadata) = !UnknownType()

  get-default-string-type(|metadata) = !UnknownType()
  get-default-short-integer-type(|metadata) = !UnknownType()
  get-default-long-integer-type(|metadata) = !UnknownType()
  get-default-decimal-type(|metadata) = !UnknownType()

  get-operation-return-type(|metadata) = !UnknownType()
  get-union-type(|metadata) = !UnknownType()

//  get-default-string-type(|metadata) = !Type("STRING")
//  get-default-short-integer-type(|metadata) = !Type("INTEGER")
//  get-default-long-integer-type(|metadata) = !Type("LONG")
//  get-default-decimal-type(|metadata) = !Type("DOUBLE")
//
//  get-count-aggregate-type(|metadata) = !Type("LONG")
//
//  get-operation-return-type(|metadata) = ?("UMin", "INTEGER"); !Type("INTEGER")
//
//  get-operation-return-type(|metadata) = ?("Cct", "STRING", "STRING"); !Type("STRING")
//
//  get-operation-return-type(|metadata) = ?("Cct", "STRING", "STRING"); !Type("STRING")
//  get-operation-return-type(|metadata) = ?("Add", "INTEGER", "INTEGER"); !Type("INTEGER")
//
//  get-operation-return-type(|metadata) = ?("MIN", "STRING"); !Type("STRING")
//  get-operation-return-type(|metadata) = ?("SUM", "INTEGER"); !Type("LONG")
//
//  get-operation-return-type(|metadata) = !Undefined()
//
//  get-vertex-labels(|metadata) = !["PERSON", "COMPANY"]
//
//  get-edge-labels(|metadata) = !["KNOWS"]
//
//  get-vertex-property-type(|metadata) = ?("PERSON", "FIRSTNAME"); !Type("STRING")
//  get-vertex-property-type(|metadata) = ?("PERSON", "AGE"); !Type("INTEGER")
//  get-vertex-property-type(|metadata) = ?("COMPANY", "AGE"); !Type("DATE")
//
//  get-edge-property-type(|metadata) = ?("KNOWS", "SINCE"); !Type("DATE")
//
//  get-vertex-property-type(|metadata) = !Undefined()
//
//  get-edge-property-type(|metadata) = !Undefined()
//
//  get-union-type(|metadata) = ?(Undefined(), <id>) + ?(<id>, Undefined())
//  get-union-type(|metadata) = (?(AmbiguousType(), _) + ?(_, AmbiguousType())); !AmbiguousType()
//
//  get-union-type(|metadata) = ?(Type("STRING"), Type("STRING")); !Type("STRING")
//
//  get-union-type(|metadata) = ?(Type("INTEGER"), Type("INTEGER")); !Type("INTEGER")
//
//  get-union-type(|metadata) = ?(Type("INTEGER"), Type("DATE"));  !AmbiguousType()
//  get-union-type(|metadata) = ?(Type("DATE"), Type("INTEGER"));  !AmbiguousType()
//
//  get-union-type(|metadata)= !Undefined()
