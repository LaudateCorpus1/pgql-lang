module common-path-expressions

language pgql-lang

test Duplicate group variable (1) [[

  SELECT n AS n
    FROM g MATCH
           (n) -[ [[e]] ]-> (m)
         , TOP 10 SHORTEST ( (n) -[ [[e]]:likes]->* (m) )

]] error like "Duplicate variable" at #1, #2

test Duplicate group variable (2) [[

  SELECT n AS n
    FROM g MATCH
           CHEAPEST ( (n) -[ [[e]] ]-> (m) )
         , TOP 10 SHORTEST ( (n) -[ [[e]]:likes]->* (m) )

]] error like "Duplicate variable" at #1, #2

test Quantifier not enclosed by SHORTEST or CHEAPEST [[

  SELECT n AS c1
    FROM g MATCH
           (n) ->[[*]] (m)
         , (n) ->[[+]] (m)
         , (n) ->[[?]] (m)
         , (n) ->[[{2}]] (m)
         , (n) ->[[{,2}]] (m)
         , (n) ->[[{2,}]] (m)
         , (n) ->[[{2,3}]] (m)

]] error like "Quantifier needs to be enclosed by ANY, ALL, SHORTEST or CHEAPEST" at #1, #2, #3, #4, #5, #6, #7

test K equals zero [[

  SELECT n AS c1 FROM g MATCH TOP [[0]] SHORTEST ( (n) ->* (m) )

]]  error like "Value should be greater than 0" at #1

test Multiple edges in SHORTEST (1) [[

  SELECT n.prop AS nProp
    FROM g MATCH SHORTEST ( (n) [[-[e1]->* (m) ->*]] (o) )

]] error like "Not yet supported: multiple edge patterns in SHORTEST or CHEAPEST" at #1

test Multiple edges in SHORTEST (2) [[

  SELECT n.prop AS nProp
    FROM g MATCH SHORTEST ( (n) (-[e1]-> () [[-[e2]->)]]* (m) )

]] error like "Not yet supported: multiple edge patterns in SHORTEST or CHEAPEST" at #1

test Multiple edges in CHEAPEST (1) [[

  SELECT n.prop AS nProp
    FROM g MATCH CHEAPEST ( (n) [[-[e1]->* (m) ->*]] (o) )

]] error like "Not yet supported: multiple edge patterns in SHORTEST or CHEAPEST" at #1

test Multiple edges in CHEAPEST (2) [[

  SELECT n.prop AS nProp
    FROM g MATCH CHEAPEST ( (n) (-[e1]-> () [[-[e2]->)]]* (m) )

]] error like "Not yet supported: multiple edge patterns in SHORTEST or CHEAPEST" at #1

test Unsupport question mark in SHORTEST [[

  SELECT *
    FROM MATCH SHORTEST ( (n) -[e1]->[[?]] (m) )

]] error like "Quantifier of the form * or + or {1,4} expected" at #1

test No quantifier in SHORTEST [[

  SELECT *
    FROM MATCH SHORTEST ( (n) [[-[e1]-> (m)]] )

]] error like "Quantifier of the form * or + or {1,4} expected" at #1

test Unsupport question mark in CHEAPEST [[

  SELECT *
    FROM MATCH CHEAPEST ( (n) (-[e1]-> COST e1.prop)[[?]] (m) )

]] error like "Quantifier of the form * or + or {1,4} expected" at #1

test No quantifier in CHEAPEST [[

  SELECT *
    FROM MATCH CHEAPEST ( (n) [[(-[e1]-> COST e1.prop)]] (m) )

]] error like "Quantifier of the form * or + or {1,4} expected" at #1

test Duplicate variables (1) [[

  SELECT n.prop AS nProp
    FROM g MATCH
           SHORTEST ( (n) -[ [[e]] ]-> (m) )
         , (n) -[ [[e]] ]-> (m)

]] error like "Duplicate variable" at #1, #2

test Duplicate variables (2) [[

  SELECT n.prop AS nProp
    FROM g MATCH
           SHORTEST ( (n) (-> ([[dst]]))* (m) )
         , ([[dst]])

]] error like "Duplicate variable" at #1, #2

test Duplicate variables (3) [[

  SELECT n.prop AS nProp
    FROM g MATCH SHORTEST ( (n) (-> ([[m]]))* ([[m]]) )

]] error like "Duplicate variable" at #1, #2

test Duplicate variables (4) [[

  SELECT n.prop AS nProp
    FROM g MATCH SHORTEST ( ([[n]]) (-[ [[n]] ]->)* (m) )

]] error like "Duplicate variable" at #1, #2

test Duplicate variables (5) [[

  SELECT n.prop AS nProp
    FROM g MATCH SHORTEST ( (n) [[(-> (m)) (m)]] )

]] error like "Duplicate variable" at #1

test Duplicate variables (6) [[

  SELECT n.prop AS nProp
    FROM g MATCH
           SHORTEST ( (n) -[ [[e]] ]->* (m) )
         , SHORTEST ( (n) -[ [[e]] ]->* (m) )

]] error like "Duplicate variable" at #1, #2

test Duplicate variables (7) [[

  SELECT n.prop AS nProp
    FROM g MATCH
           SHORTEST ( (n) (-> ([[x]]))* (m) )
         , SHORTEST ( (n) -[ [[x]] ]->* (m) )

]] error like "Duplicate variable" at #1, #2

test Duplicate variables (8) [[

  SELECT e AS c1
   FROM g MATCH SHORTEST ( (n) -[e]->* (m) )
   GROUP BY n AS [[e]]

]] error like "Duplicate variable: variable already defined in the MATCH clause" at #1

test Duplicate variables (9) [[

  SELECT dst AS c1
    FROM g MATCH SHORTEST ( (n) (-> (dst))* (m) )
   GROUP BY n AS [[dst]]

]] error like "Duplicate variable: variable already defined in the MATCH clause" at #1

test Nested regex (1) [[

    PATH p AS SHORTEST ( (n) -[e]->* (m) )
  SELECT n AS c1
    FROM g MATCH (n)

]] error like "Nested regular expressions not supported"

test Nested regex (2) [[

    PATH p AS (n) -/:likes/-> (m)
  SELECT n AS c1
    FROM g MATCH (n)

]] error like "Nested regular expressions not supported"

test Nested regex (3) [[

  SELECT n AS c1
    FROM g MATCH SHORTEST ( (n) [[-/:likes/->]]* (m) )

]] error like "Nested regular expressions not supported" at #1

test Multiple group variables in aggregation (1) [[

  SELECT [[MIN(e.prop + x.prop)]] AS aggr1
       , [[MAX(e.prop + x.prop)]] AS aggr2
       , [[COUNT(e.prop + x.prop)]] AS aggr3
       , [[SUM(e.prop + x.prop)]] AS aggr4
       , [[AVG(e.prop + x.prop)]] AS aggr5
       , [[ARRAY_AGG(e.prop + x.prop)]] AS aggr6
       , [[LISTAGG(e.prop + x.prop, ';')]] AS aggr7
    FROM g MATCH SHORTEST ( (n) (-[e]-> (x))* (m) )

]] error like "Accessing multiple group variables in a single aggregation is not yet supported" at #1, #2, #3, #4, #5, #6, #7

test Multiple group variables in aggregation (2) [[

  SELECT [[MIN(e1.prop + e2.prop)]] AS aggr1
       , [[MAX(e1.prop + e2.prop)]] AS aggr2
       , [[COUNT(e1.prop + e2.prop)]] AS aggr3
       , [[SUM(e1.prop + e2.prop)]] AS aggr4
       , [[AVG(e1.prop + e2.prop)]] AS aggr5
       , [[ARRAY_AGG(e1.prop + e2.prop)]] AS aggr6
       , [[LISTAGG(e1.prop + e2.prop, ';')]] AS aggr7
    FROM g
      MATCH
        SHORTEST ( (n) -[e1]->* (m) ),
        SHORTEST ( (n) -[e2]->* (m) )

]] error like "Accessing multiple group variables in a single aggregation is not yet supported" at #1, #2, #3, #4, #5, #6, #7

test Missing cost clause (1) [[

  SELECT 1 AS c1
    FROM g
   MATCH CHEAPEST ( (n) ([[-[e1]->)]]* (m) )

]] error like "COST clause expected after pattern" at #1

test Missing cost clause (2) [[

  SELECT 1 AS c1
    FROM g
   MATCH CHEAPEST ( (n) (-[e1]-> WHERE [[e1.cost > 0]])* (m) )

]] error like "COST clause expected after WHERE clause" at #1

test Cross constraint in COST clause not yet supported (1) [[

  SELECT 1 AS c1
    FROM g
   MATCH CHEAPEST ( (n) ((src) -[e]-> (dst) COST [[src.prop + dst.prop]])* (m) )

]] error like "Not yet supported: expressions with multiple variables" at #1

test Cross constraint in COST clause not yet supported (2) [[

  SELECT 1 AS c1
    FROM g
   MATCH CHEAPEST ( (n) ((src) -[e]-> (dst) COST [[e.prop + dst.prop]])* (m) )

]] error like "Not yet supported: expressions with multiple variables" at #1

test Cost clause in SHORTEST [[

  SELECT 1 AS c1
    FROM g
   MATCH SHORTEST ( (n) (-[e1]-> [[COST e1.prop]])* (m) )

]] error like "COST clause only allowed in combination with CHEAPEST" at #1

test Label disjunction in path [[

  SELECT 1 AS c1
    FROM MATCH (n) -/:[[likes|dislikes]]*/-> (m)

]] error like "Label disjunction not supported here" at #1
